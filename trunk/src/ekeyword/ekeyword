#!/usr/bin/perl -w
#
# Copyright 2003, Gentoo Technologies, Inc.
# Author: Aron Griffis <agriffis@gentoo.org>
#
# ekeyword: Update the KEYWORDS in an ebuild.  For example:
#
#   $ ekeyword ~alpha oaf-0.6.8-r1.ebuild
#   12c12
#   < KEYWORDS="x86 ppc sparc"
#   ---
#   > KEYWORDS="x86 ppc sparc ~alpha"


my ($kw_re) = '^[-~]?\w+$';

# make sure the cmdline consists of keywords and ebuilds
unless (@ARGV > 1 && $ARGV[0] =~ /$kw_re/o) {
    die "syntax: ekeyword { arch | ~arch | -arch } ebuild...\n" 
}
for my $a (@ARGV) {
    next if $a =~ /$kw_re/o;		# keyword
    next if $a =~ /^\S+\.ebuild$/;	# ebuild
    die "I don't understand $a\n";
}

for my $f (@ARGV) {
    if ($f =~ /$kw_re/o) {
	push @kw, $f;
	next;
    }

    open I, "<$f"       or die "Can't read $f: $!\n";
    open O, ">$f.new"   or die "Can't create $f.new: $!\n";
    select O;

    while (<I>) {
	/^KEYWORDS/ or print, next;
	for my $k (@kw) {
	    (my $arch = $k) =~ s/^[-~]//;
	    s/[-~]?$arch/$k/ || s/(.*?['"].*?)\s*(?=['"])/$1 $k/;
	}
	print $_, <I> or die "Can't write $f.new: $!\n";
    }

    close I;
    close O;

    system "diff $f $f.new"; # don't die because the files might be the same
    rename "$f.new", "$f"       or die "Can't rename: $!\n";
}
